#!/usr/bin/perl -w

use strict;
use utf8;
use Bot4;
use Data::Dumper;

my $timestamp;
my $rcid;

my $bot = new Bot4;
$bot->single(1);
$bot->addOption( "timestamp=s" => \$timestamp );
$bot->addOption( "rcid=i"      => \$rcid );

$bot->setProject( "wikipedia", "pl" );
$bot->setup;

my $logger = Log::Any::get_logger;
$logger->info("Start");

my $api = $bot->getApi;
$api->checkAccount;

# tools.wikimedia.pl ma rozsynchronizowany zegar
# funkcja pobiera czas z serwerów wikipedii
my $currentTime = $api->expandtemplates( 'text' => '{{#time:U}}' );
die "Nie mogę pobrać czasu z serwerów WMF\n"
  unless defined $currentTime;

my $storable = $bot->retrieveData();

my $rcend = to_wiki_timestamp( $currentTime - 15 * 60 );
$timestamp = $storable->{timestamp}
  unless defined $timestamp;
$rcid = $storable->{rcid}
  unless defined $rcid;
$rcid ||= 0;

my $baseRcid = $rcid;

$logger->info( "Aktualny czas na wiki: " . to_wiki_timestamp($currentTime) );
$logger->info("Sprawdzanie nowych od $timestamp do $rcend");

my $iterator = $api->getIterator(
	'list'        => 'recentchanges',
	'rctype'      => 'new',
	'rcnamespace' => '0',               # '0|4|12|14|100|102',
	'rclimit'     => 'max',
	'rcdir'       => 'newer',
	'rcstart'     => $timestamp,
	'rcend'       => $rcend,
);

while ( my $item = $iterator->next ) {
	next if $item->{rcid} <= $baseRcid;
	next if $item->{timestamp} gt $rcend;

	$timestamp = $item->{timestamp}
	  if $timestamp lt $item->{timestamp};

	$rcid = $item->{rcid}
	  if $rcid < $item->{rcid};

	$logger->info("Sprawdzanie [[$item->{title}]]");
	$bot->status("Sprawdzanie [[$item->{title}]]");

	my $data = $api->query(
		'prop'        => 'links|templates|categories|revisions|info',
		'intoken'     => 'edit',
		'plnamespace' => 0,
		'pllimit'     => 'max',
		'titles'      => $item->{title},
		'rvlimit'     => 1,
		'rvdir'       => 'older',
		'rvprop'      => 'content|timestamp',
		'tllimit'     => 'max',
		'cllimit'     => 'max',
		'maxlag'      => 20,
	);

	delete $data->{'query-continue'}{'revisions'}
	  if exists $data->{'query-continue'}{'revisions'};

	my ($page) = values %{ $data->{query}->{pages} };

	my ($revision) = values %{ $page->{revisions} };
	my $content = $revision->{"*"};

	if ( !defined $content or $content eq '' ) {
		$logger->info("* pusta strona?");
		next;
	}

	if ( exists $page->{redirect} ) {
		$logger->info("* jest przekierowaniem");
		next;
	}

	if ( $content =~ /^#REDIRECT/i ) {
		$logger->info("* jest przekierowaniem?!?");
		next;
	}

	my %categories;
	my $categories = 0;

	if ( $page->{categories} ) {
		%categories = map { $_->{title} => 1 } values %{ $page->{categories} };
		delete $categories{"Kategoria:Automatyczne wykrywanie NPA"};
		foreach my $title ( keys %categories ) {
			delete $categories{$title} if $title =~ /^Kategoria:(?:Urodzeni|Zmarli) w \d+$/;
		}

		$categories = scalar keys %categories;
	}

	if ( exists $categories{"Kategoria:Ekspresowe kasowanie"} ) {
		$logger->info("* do ekspresowego kasowania");
		next;
	}

	my $templates = $page->{templates} ? scalar keys %{ $page->{templates} } : 0;
	my $links     = $page->{links}     ? scalar keys %{ $page->{links} }     : 0;

	$logger->info("* kategorii: $categories");
	$logger->info("* szablonów: $templates");
	$logger->info("* linków: $links");

	$links++ if exists $categories{"Kategoria:Linki wewnętrzne do dodania"};
	$links++ unless $page->{ns} == 0;

	if ( $categories and $links ) {
		$logger->info("* nic do roboty!");
		next;
	}

	my @templates;
	my @summary;
	unless ($categories) {
		if ( $page->{ns} != NS_MAIN and $page->{ns} != NS_CATEGORY ) {
			push @templates, "<noinclude>{{kategoria}}</noinclude>";
		}
		else {
			push @templates, "{{kategoria}}";
		}
		push @summary, "{{[[Template:kategoria|kategoria]]}}";
	}
	unless ($links) {
		push @templates, "{{linki}}";
		push @summary,   "{{[[Template:linki|linki]]}}";
	}
	local $" = ', ';

	$bot->status("Oznaczanie [[$item->{title}]]");

	$logger->info("* dodaję: @templates");
	my $summary = "Sprawdzanie nowych stron: @summary";
	$api->edit(
		'title'          => $page->{title},
		'token'          => $page->{edittoken},
		'starttimestamp' => $page->{starttimestamp},
		'summary'        => $summary,
		'minor'          => 1,
		'bot'            => 1,
		'nocreate'       => 1,
		'prependtext'    => join( '', @templates ) . "\n",
	);
}

$storable->{timestamp} = $timestamp;
$storable->{rcid}      = $rcid;

$bot->storeData($storable);

# perltidy -et=8 -l=0 -i=8
