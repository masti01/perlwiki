#!/usr/bin/perl -w

use strict;
use utf8;
use Bot4;
use Log::Any;
use Data::Dumper;
use DBI;

# Jak pobrać informacje o tym, czy wersja została przejrzana?

my $db  = 'var/rc.sqlite';
my $all = 0;
my $bot = new Bot4;
$bot->single(1);
$bot->addOption( "database=s", \$db,  "Changes path to a database" );
$bot->addOption( "all",        \$all, "Imports all changes" );
$bot->setup;

my $logger = Log::Any::get_logger;
$logger->info("Start");

my $dbh = DBI->connect( "dbi:SQLite:dbname=$db", "", "", { RaiseError => 1, PrintError => 0, sqlite_use_immediate_transaction => 1 } );

my @projects = (    #
	{           #
		'family' => 'wikipedia',
		'lang'   => 'pl',
	},
	{           #
		'family' => 'wikisource',
		'lang'   => 'pl',
	},
	{           #
		'family' => 'wikibooks',
		'lang'   => 'pl',
	},
	{           #
		'family' => 'wiktionary',
		'lang'   => 'pl',
	},
	{           #
		'family' => 'wikinews',
		'lang'   => 'pl',
	},
	{           #
		'family' => 'wikiquote',
		'lang'   => 'pl',
	},
);

sub fetchData {
	my $selectProject   = $dbh->prepare('SELECT project_id FROM projects WHERE project_name = ?');
	my $insertProject   = $dbh->prepare('INSERT INTO projects (project_name) VALUES(?)');
	my $selectLastRevid = $dbh->prepare('SELECT MAX(rc_newrevid) FROM recentchanges WHERE rc_project = ?');
	my $insertEdit      = $dbh->prepare('INSERT OR IGNORE INTO recentchanges(rc_project, rc_ns, rc_title, rc_oldrevid, rc_newrevid, rc_user, rc_timestamp, rc_comment) VALUES(?, ?, ?, ?, ?, ?, ?, ?)');

	foreach my $project (@projects) {
		$logger->info("Downloading pages from $project->{lang}.$project->{family}...");

		my $api = $bot->getApi( $project->{family}, $project->{lang} );
		$api->checkAccount;

		my $projectId;
		$selectProject->execute("$project->{lang}.$project->{family}");
		unless ( ($projectId) = $selectProject->fetchrow_array ) {
			$insertProject->execute("$project->{lang}.$project->{family}");
			$projectId = $dbh->last_insert_id( "", "", "", "" );
		}

		$selectLastRevid->execute($projectId);
		my ($lastRevid) = $selectLastRevid->fetchrow_array;
		$lastRevid ||= 0;

		my $iterator = $api->getIterator(
			'action'  => 'query',
			'list'    => 'recentchanges',
			'rcshow'  => 'anon',
			'rctype'  => 'edit',
			'rcprop'  => 'user|userid|timestamp|ids|title|comment',    # sizes
			'rclimit' => $all ? 'max' : '100',
		);

		$dbh->begin_work;
		while ( my $entry = $iterator->next ) {
			if ( $logger->is_debug ) {
				$logger->debug( Dumper($entry) );
			}
			last
			  if !$all and $entry->{revid} <= $lastRevid;
			$insertEdit->execute( $projectId, @{$entry}{ 'ns', 'title', 'old_revid', 'revid', 'user', 'timestamp', 'comment' } );
		}
		$dbh->commit;
	}
}

fetchData;
